#include <old_DJ_head_h.krnk>

# ===================================================================
# Library: libs\game\custommovement\doublejump\djump_old\djump_old_server.krnk
# Author: blandrice
# ===================================================================


num timeNow = 0;

# Runs every game tick
public action update(num delta) {
    timeNow = GAME.TIME.now();
}

public action onPlayerUpdate(str id, num delta, obj inputs) {
    timeNow = GAME.TIME.now();
    obj player = GAME.PLAYERS.findByID(id);
    
    if ((bool) inputs.crouch) {
        player.tLastCrouch = timeNow;
    }
    if ((bool) player.onGround) {
        player.jumpCnt = old_DJ_head_h_MAX_JUMPCOUNT-1;
    } else if (old_DJ_head_h_WALLJUMP_REFRESHS && (bool) player.onWall && (bool) inputs.jump) {
        player.jumpCnt = old_DJ_head_h_MAX_JUMPCOUNT-1;

    }
    if((bool) inputs.jump) {
        if((bool)player.jumpPressed == false && (num) player.jumpCnt > 0 && !(bool) player.onGround) {
            if (!(bool) player.onWall && old_DJ_head_h_WALLJUMP_REFRESHS) {
                if (old_DJ_head_h_ENABLE_CROUCHJUMP && (timeNow - ((num) player.tLastCrouch)) < old_DJ_head_h_MS_DURATION_UNCROUCH) {
                    player.velocity.y = Math.lerp(old_DJ_head_h_HEIGHT_CROUCHJUMP,old_DJ_head_h_HEIGHT_JUMP,(timeNow - ((num) player.tLastCrouch))/old_DJ_head_h_MS_DURATION_UNCROUCH);	
                } else {
                    player.velocity.y = old_DJ_head_h_HEIGHT_JUMP;	    
                }
                (num) player.jumpCnt--;
            }
        }
        player.jumpPressed = true;
	} else {
        player.jumpPressed = false;
    }
}