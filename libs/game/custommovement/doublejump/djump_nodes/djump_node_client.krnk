# ===================================================================
# File: libs\game\custommovement\doublejump\djregistersync_nodes\djump_node_client.krnk
# Author: blandrice
# Description: Doublejump with crystals
# ===================================================================

#include <djump_node.krnk>

num COOLDOWN = 2000;
num W = 0.3;
str COLOR_JUMP = "#E2FF00";

obj[] nodeslist = obj[];

action dummy_Callback(){
}

public action start() {
    # add nodes to obj list
    obj[] tnodes = GAME.OBJECTS.getPathNodes();
    for (num i = 0; i < lengthOf tnodes; i++) 
    {
        obj node = tnodes[i];
        # obj nodeobj = GAME.SCENE.addCube("32616g", "0xfff", (num)node.x, (num)node.y, (num)node.z, W, W, W, {});
        obj nodeobj = GAME.SCENE.addAsset("32616g", (num)node.x, (num)node.y, (num)node.z, W, "0xfff", {opacity:1}, dummy_Callback);
        addTo nodeslist nodeobj;
    }
}

num djalpha = 0;
# Green indicator you have a doublejump stored
public action render(num delta) {
    obj size = GAME.OVERLAY.getSize(); 
    obj plr = GAME.PLAYERS.getSelf();

    # alpha of dj
    num finalalpha = (num)plr.jumpCount > 0 ? 0.1 : 0;
    if (Math.abs(djalpha- finalalpha) > 0.01) {
        djalpha +=((finalalpha-djalpha) * 0.006 * delta);
    }

    GAME.OVERLAY.drawRect(0,0,(num)size.width,(num)size.height,0,COLOR_JUMP,djalpha,false);
    # GAME.OVERLAY.drawRect(x, y, w, h, r, color, opac, center)
}


#include <DJcfg_nodes_h.krnk>
#include <AABB.krnk>

public action onPlayerSpawn(str id) {
	obj player = GAME.PLAYERS.findByID(id);

    # nodes
    player.nodes = GAME.OBJECTS.getPathNodes();
    for (num i = 0; i < lengthOf (obj[]) player.nodes; i++) {
        obj[] nodes = (obj[]) player.nodes;
        obj inode = nodes[i];
        inode.tCooldown = 0;
        inode.scale = {x:5,y:5,z:5};
        inode.position = {x:inode.x,y:inode.y,z:inode.z};
    }
    # player jump
    player.timeLastCrouched = 0;
    player.jumpCount = 0;
    player.hasJumped = false;
    player.registerSyncValues("timeLastCrouched");
    player.registerSyncValues("timeLastOnGround");
    player.registerSyncValues("jumpCount");
    player.registerSyncValues("hasJumped");
    player.registerSyncValues("nodes");

    
}

public action onPlayerUpdate(str id, num delta, obj inputs) {
	obj player = GAME.PLAYERS.findByID(id);
    
    # crouch
    if ((bool) player.crouch){
        player.scale = 
        {
            x:AABB_w,
            y:AABB_c,
            z:AABB_w
        };
    } else {
        player.scale=
        {
            x:AABB_w,
            y:AABB_H,
            z:AABB_w
        };
    }
    # collisions with nodes
    obj[] collisions = AABB_detectCollisions(player,(obj[])player.nodes);
    if (lengthOf collisions > 0)
    {
        for (num i = 0; i < lengthOf collisions; i++)
        {
            obj colobj = collisions[i];
            if ((num)colobj.tCooldown == 0)
            {
                # add jump
                (num)player.jumpCount+=1;
                # increase tCooldown to refresh rate
                colobj.tCooldown = COOLDOWN;
                # acquire jump sound
                if ((num) DJcfg_nodes_h_c.ACQUIREJUMPSOUND > 0) {
                    GAME.SOUND.play2D((num) DJcfg_nodes_h_c.ACQUIREJUMPSOUND, 1, 1, false); 
                }
            }
        }
    }
    # decrement nodes cooldown and color
    for (num i = 0; i < lengthOf (obj[]) player.nodes; i++) {
        obj inode = ((obj[]) player.nodes)[i];
        (num)inode.tCooldown -= delta;
        if ((num)inode.tCooldown < 0)
        {
            inode.tCooldown=0;            
        }
        nodeslist[i].opacity = ((num) inode.tCooldown > 0) ? 0.1 : 1;
    }
    
    # Double-Jump stuff

    # crouch timestamp
    if ((bool) inputs.crouch) 
    {
        player.timeLastCrouched = 0;
    } 
    else 
    {
        (num) player.timeLastCrouched += delta;
    }
    # reset jumpcount on ground
    if((bool) player.onGround) 
    {
        player.jumpCount = (num) DJcfg_nodes_h_c.MAX_JUMPCOUNT-1;
        player.timeLastOnGround = 0;
    } 
    else
    {
        (num) player.timeLastOnGround += delta;
    } 
    #  reset jumpCount after walljump
     if ((bool) DJcfg_nodes_h_c.WALLJUMP_REFRESHS && (bool) player.onWall && (bool) inputs.jump) 
    {
        player.jumpCount = (num) DJcfg_nodes_h_c.MAX_JUMPCOUNT-1;
    } 
    # input jump logic
    if((bool) inputs.jump) {
        if(!(bool)player.hasJumped  && (num)player.jumpCount > 0 && !(bool)player.onGround && ((num)player.timeLastOnGround > 150)) 
        {
            if (!(bool) player.onWall && (bool)DJcfg_nodes_h_c.WALLJUMP_REFRESHS) 
            {
                if ((bool) DJcfg_nodes_h_c.ENABLE_CROUCHJUMP && ((num)player.timeLastCrouched < (num)DJcfg_nodes_h_c.MS_DURATION_UNCROUCH)) 
                {
                    player.velocity.y = Math.lerp((num) DJcfg_nodes_h_c.VEL_CROUCHJUMP,(num) DJcfg_nodes_h_c.VEL_JUMP,(num)player.timeLastCrouched/(num) DJcfg_nodes_h_c.MS_DURATION_UNCROUCH);	# "moonjumping" calculation
                } else 
                {
                    player.velocity.y = (num) DJcfg_nodes_h_c.VEL_JUMP;	
                }
                if ((num) DJcfg_nodes_h_c.DJUMPSOUND > 0) 
                {
                    GAME.SOUND.play2D((num) DJcfg_nodes_h_c.DJUMPSOUND, 1, 0.5, false); # jump DJcfg_nodes_h_c.SOUND
                }

                (num)player.jumpCount--;
            }
        }
        player.hasJumped = true;
    }
    else {
        player.hasJumped = false;
    }
}