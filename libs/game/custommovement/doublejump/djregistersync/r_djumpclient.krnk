# ===================================================================
# File: libs\doublejump\djregistersync\r_djumpclient.krnk
# Author: blandrice
# Description: double-jump using registersync feature
# ===================================================================

#include <DJconfig_h.krnk>

# Add this both on the server and client side.

public action onPlayerSpawn(str id) {
	obj player = GAME.PLAYERS.findByID(id);
    player.timeLastCrouched = false;
    player.jumpCount = false;
    player.hasJumped = false;

    player.registerSyncValues("timeLastCrouched");
    player.registerSyncValues("timeLastOnGround");
    player.registerSyncValues("jumpCount");
    player.registerSyncValues("hasJumped");
}

public action onPlayerUpdate(str id, num delta, obj inputs) {
	obj player = GAME.PLAYERS.findByID(id);
	
    # Double-Jump stuff

    # crouch timestamp
    if ((bool) inputs.crouch) 
    {
        player.timeLastCrouched = 0;
    } 
    else 
    {
        (num) player.timeLastCrouched += delta;
    }
    # reset jumpcount on ground
    if((bool) player.onGround) 
    {
        player.jumpCount = (num) DJconfig_h_c.MAX_JUMPCOUNT-1;
        player.timeLastOnGround = 0;
    } 
    else
    {
        (num) player.timeLastOnGround += delta;
    } 
    #  reset jumpCount after walljump
     if ((bool) DJconfig_h_c.WALLJUMP_REFRESHS && (bool) player.onWall && (bool) inputs.jump) 
    {
        player.jumpCount = (num) DJconfig_h_c.MAX_JUMPCOUNT-1;
    } 
    # input jump logic
    if((bool) inputs.jump) {
        if(!(bool)player.hasJumped  && (num)player.jumpCount > 0 && !(bool)player.onGround && ((num)player.timeLastOnGround > 150)) 
        {
            if (!(bool) player.onWall && (bool)DJconfig_h_c.WALLJUMP_REFRESHS) 
            {
                if ((bool) DJconfig_h_c.ENABLE_CROUCHJUMP && ((num)player.timeLastCrouched < (num)DJconfig_h_c.MS_DURATION_UNCROUCH)) 
                {
                    player.velocity.y = Math.lerp((num) DJconfig_h_c.VEL_CROUCHJUMP,(num) DJconfig_h_c.VEL_JUMP,(num)player.timeLastCrouched/(num) DJconfig_h_c.MS_DURATION_UNCROUCH);	# "moonjumping" calculation
                } else 
                {
                    player.velocity.y = (num) DJconfig_h_c.VEL_JUMP;	
                }
                if ((num) DJconfig_h_c.SOUND > 0) 
                {
                    GAME.SOUND.play2D(32576, 1, 0.5, false); # jump DJconfig_h_c.SOUND
                }

                (num)player.jumpCount--;
            }
        }
        player.hasJumped = true;
    }
    else {
        player.hasJumped = false;
    }
}