#include <g.krnk>
#include <plrutil_s.krnk>
# ===================================================================
# Library: libs\utils\plrbehavior\plrbhvr_s.krnk
# Author: blandrice
#   Description: disable server sync + behavior for custom movement
#   send plr position/vel to server manually
# ===================================================================

num tlastbroadcast = 0;
num INTERVAL_BROADCAST = 100;
str[] plrsleft = str[];

public action start(){
    GAME.DEFAULT.disableServerSync();
	GAME.DEFAULT.disablePlayerBehaviour();
}

public action onNetworkMessage(str id, obj data, str playerID) {
    obj plr = GAME.PLAYERS.findByID(playerID);
    if (id == "plrbhvr"){
        plr.position.x = (num)data.position.x;
        plr.position.y = (num)data.position.y;
        plr.position.z = (num)data.position.z;
        plr.velocity.x = (num)data.velocity.x;
        plr.velocity.y = (num)data.velocity.y;
        plr.velocity.z = (num)data.velocity.z;
        plr.rotation.x = (num)data.rotation.x;
        plr.rotation.y = (num)data.rotation.y;
    }   
}

public action update(num delta) {
    obj[] plrs = GAME.PLAYERS.list();
    obj[] plrsposrot = obj[];
    if ((g_tnow - tlastbroadcast) > INTERVAL_BROADCAST){
        tlastbroadcast = g_tnow;

        for (num i = 0; i < lengthOf plrs; i++){
            obj plr = plrs[i];
            addTo plrsposrot {
                n: (str)plr.username,
                id: (str)plr.id,
                pos:{
                    x:Math.round((num)plr.position.x),
                    y:Math.round((num)plr.position.y),
                    z:Math.round((num)plr.position.z)
                },
                rot: {
                    x:Math.roundDecimal((num)plr.rotation.x,2),
                    y:Math.roundDecimal((num)plr.rotation.y,2)
                }
            };
        }
        GAME.NETWORK.broadcast("plrs", {plrs:plrsposrot,left:plrsleft});
        plrsleft = str[];
    }
    
    
}

# public action onPlayerSpawn(str id) {}
# public action onPlayerDeath(str id, str killerID) {}
public action onPlayerLeave(str playerID) {
    GAME.log("player left");
    addTo plrsleft playerID;

}

public action onGameEnd(){
    GAME.NETWORK.broadcast("clrplrs", {});
}