# ===================================================================
# Header: \strutils\strutils.krnk
# Author: blandrice, ProfNoob (replaceall), ManiaMapper (Splice)
# Description: Additional String utilities
# ===================================================================
str action truncate(str text, num ln) {
    str ret = "";
    for (num i = 0; i < ln; i++) {
        ret += text[i];
    }
    return ret;
}

str action splice(str input, num start, num end) {
    num charsLen = lengthOf input;
    if(charsLen == 0 || start >= charsLen || start >= end) {
        return '';
    }
    if(end > charsLen) {
        end = charsLen;
    }
    str output = '';
    for(num i = start; i < end; i++) {
        output += input[i];
    }
    return output;
}

bool action starts(str text,str value) {
    if (UTILS.textContains(truncate(text, lengthOf value), value)) {return true;} 
	return false;}

str[] action split(str s, str delim) {
	str[] output = str[];
	num idx_matchstart = 0;

    # iterate over string
    for(num i = 0; i < lengthOf s; i++) {
        bool match = false;
        
        # match delimiter
        for (num j = 0; j < lengthOf delim; j++) {
			match = true;
            if (s[i+j] != delim[j]) {
				match = false; break;
			}
		}

        # matches, parse out the string previous
        if (match) { 
            str word = "";
            for (num j = idx_matchstart; j < i; j++) {
                word += s[j];
            }
            addTo output word;

            # skip delimiter
            i += lengthOf delim - 1;
            idx_matchstart = i+1;
        }
    } 
    
    # also parse last string
    str word = "";
    for (num i = idx_matchstart; i < lengthOf s; i++) {
        word += s[i];
    }
    addTo output word;

	return output;
}

str action replace_all(str string, str to_replace, str replace_with){
    while(UTILS.replaceText(string, to_replace, replace_with) != string){
        string = UTILS.replaceText(string, to_replace, replace_with);
    }
    return string;
}
